REM *******************************************************************************
REM *        SCOPE: Any                                                           *
REM *  DESCRIPTION: Southland Library STRING Manipulation Functions               *
REM *-----------------------------------------------------------------------------*
REM *   CREATED BY: D.Young - Southland Industries/Garden Grove                   *
REM *       DIRECT: (714) 230-7993                                                *
REM *         CELL: (949) 701-0724                                                *
REM *        EMAIL: dyoung@southlandind.com                                       *
REM *         DATE: 03-01-2013                                                    *
REM *   REVISED BY:                                                               *
REM *       DIRECT: (xxx) xxx-xxxx                                                *
REM *         CELL: (xxx) xxx-xxxx                                                *
REM *        EMAIL: <user>@southlandind.com                                       *
REM *         DATE: mm-dd-yyyy                                                    *
REM *******************************************************************************


REM *******************************************************************************
REM *     FUNCTION: Convert Decimal to Feet & Inch STRing                         *
REM *   ARGUEMENTS: N = Decimal {number}                                          *
REM *               R = Rounding {number}                                         *
REM * RETURN VALUE: {string}                                                      *
REM * DEPENDENCIES: SILIB (Math).COD                                              *
REM *******************************************************************************
function FISTR(N, R)
   include mappath_scripts + "\SILIB (MATH).COD"

   REM - Declare Variables
   dim S
   dim FT
   dim IN
   dim FR
   dim TN
   dim FRLEN
   dim FRGCD
   dim FRMUL
   dim RT

   REM - Store Number Sign and Convert To Positive for manipulation
   S = sign(N)
   N = N * S

   REM - Verify Rounding Argument is Positive
   R = R * sign(R)

   REM - Round Number Arguement (16 would round to nearest 1/16th inch)
   N = round(N*R,0)/R

   REM - Calculate Feet
   FT = rounddown(N/12,0)

   REM - Store Temp Number (minus feet)
   TN = N-(FT*12)

   REM - Calculate Inches
   IN = rounddown(TN,0)

   REM - Store Temp Number (minus full inches)
   TN = TN - IN

   REM - Save Fractional Decimal for Manipulation
   FR = TN

   REM - Determine Length of Fraction (note: "0." count as 2)
   REM - to calculate multiplier
   FRLEN = len(FR)
   if FRLEN = 3 then
      FRMUL = 10
   elseif FRLEN = 4 then
      FRMUL = 100
   elseif FRLEN = 5 then
      FRMUL = 1000
   elseif FRLEN = 6 then
      FRMUL = 10000
   else
      FRMUL = 0
   endif

   REM - Multiply Decimal by Proper Multiplier
   FR = FR * FRMUL
 
   REM - Calculate Greatest Common Divisor for Fraction Simplification
   FRGCD = GCD(FR, FRMUL)
 
   REM - Build Feet/Inch/Fraction String
   if FRMUL=0 then
      RT = FT*S + "' " + IN + ascii(34)
   else
      RT = FT*S + "' " + IN + "-" + FR/FRGCD + "/" + FRMUL/FRGCD + ascii(34)
   endif

   REM - Return to Calling Function
   return RT
end function
